import org.jetbrains.compose.desktop.application.dsl.TargetFormat

plugins {
    alias libs.plugins.android.application
    alias libs.plugins.compose.compiler
    alias libs.plugins.google.ksp
    alias libs.plugins.jetbrains.compose
    alias libs.plugins.kotlin.multiplatform
    alias libs.plugins.kotlin.serialization
}

kotlin {
    jvmToolchain(17)
    androidTarget()
    jvm("desktop")

    sourceSets {
        androidMain {
            dependencies {
                implementation libs.androidx.core.ktx
                implementation libs.androidx.activity.compose
                implementation libs.coil.coil3.network.okhttp
            }
        }

        commonMain {
            dependencies {
                implementation compose.material
                implementation compose.ui
                implementation libs.androidx.room.runtime
                implementation libs.androidx.sqlite.bundled
                implementation libs.coil.coil3.compose
                implementation libs.jetbrains.lifecycle.runtime.compose
                implementation libs.jetbrains.lifecycle.viewmodel.compose
                implementation libs.jetbrains.navigation.compose
                implementation libs.kotlinx.collections.immutable
                implementation libs.kotlinx.coroutines.core
                implementation libs.kotlinx.datetime
                implementation libs.kotlinx.serialization.core
                implementation libs.tatarka.inject.runtime
            }
        }

        desktopMain {
            dependencies {
                implementation compose.desktop.currentOs
                implementation libs.coil.coil3.network.okhttp
                implementation libs.kotlinx.coroutines.swing
            }
        }
    }
}

android {
    compileSdk 34
    namespace "me.oikvpqya.playground"

    defaultConfig {
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
        }
    }

    buildFeatures {
        compose true
    }
}

dependencies {
    kspAndroid libs.androidx.room.compiler
    kspDesktop libs.androidx.room.compiler
    kspAndroid libs.tatarka.inject.compiler
    kspDesktop libs.tatarka.inject.compiler
}

compose {
    desktop {
        application {
            mainClass = "MainKt"

            nativeDistributions {
                targetFormats(TargetFormat.Dmg, TargetFormat.Msi, TargetFormat.Deb)
                packageName = "me.oikvpqya.playground"
                packageVersion = "1.0.0"
            }
        }
    }
}
